
-- =============================================
-- Author:		Iftekharul Alam Khan Lodi
-- Create date: 02-Mar-13
-- Description:	Authorize record to NCollection
-- =============================================
ALTER PROCEDURE [dbo].[CMS_NCollection_Auth]	
	@NONZONE_REF	varchar(7),	
	@MOD_NO			int,    
	@MOD_DATETIME	datetime
AS
BEGIN	
	DECLARE @STATUS				char(1),
			@RTN_STATUS			char(1),
			@RTN_CODE			varchar(2),
			@CC_RTN_STATUS		char(1),
			@CC_RTN_CODE		varchar(2),	
			@REF_DATE			datetime,
			@AMOUNT				numeric(12,2),
			@DDREFNO			varchar(15),
			@INPUT_BY			varchar(20),
			@INPUT_DATETIME		datetime,
			@INPUT_FROM			varchar(30),			
			@VAT				numeric(12, 2),
			@COMMISION			numeric(12, 2),
			@POSTAGE_CHARGE		numeric(12, 2),
			@INSTRUMENT_EXP		numeric(12, 2),				
			@IS_AUTH			bit,
			@D_CODE				varchar(12);
	
	DECLARE @ErrorVar1		int; 
			
	SET NOCOUNT ON;

	SELECT @STATUS = '';

	BEGIN TRANSACTION;

	IF EXISTS(SELECT NONZONE_REF from NCOLLECTION_HIST 
			  WHERE NONZONE_REF=@NONZONE_REF AND MOD_NO=@MOD_NO)
		BEGIN
			SELECT @REF_DATE=REF_DATE, @DDREFNO=DDREFNO, @AMOUNT=AMOUNT, @VAT=VAT, 
				@COMMISION=COMMISION, @POSTAGE_CHARGE=POSTAGE_CHARGE, 
				@INSTRUMENT_EXP=INSTRUMENT_EXP, @RTN_STATUS=RTN_STATUS, 
				@RTN_CODE=RTN_CODE, 
				@INPUT_BY=INPUT_BY,	@INPUT_DATETIME=INPUT_DATETIME,
				@INPUT_FROM=INPUT_FROM,@IS_AUTH=IS_AUTH,@STATUS=[STATUS]
				FROM NCOLLECTION_HIST 
				WHERE NONZONE_REF=@NONZONE_REF AND MOD_NO=@MOD_NO;
			IF (@IS_AUTH = 1)
				BEGIN
					-- already authorized
					ROLLBACK TRANSACTION;
					RETURN 3;
				END;
			IF (@INPUT_BY = substring(suser_name(),charindex('\',suser_name())+1,20))
				BEGIN
					-- maker and checker same person
					ROLLBACK TRANSACTION;
					RETURN 5;
				END;
			IF (@INPUT_DATETIME <> @MOD_DATETIME)
				BEGIN
					-- Data hase been changed. Need notification
					ROLLBACK TRANSACTION;
					RETURN 7;
				END;			
		END;
	ELSE
		BEGIN			
			-- not exist
			ROLLBACK TRANSACTION;
			RETURN 4;
		END	;

	IF @MOD_NO > 1
		BEGIN	
		
			INSERT INTO NCOLLECTION_HIST 
				SELECT *,1 FROM NCOLLECTION
				WHERE NONZONE_REF=@NONZONE_REF;
			
			SELECT @ErrorVar1 = @@error;

			IF @ErrorVar1 <> 0
				BEGIN
					ROLLBACK TRANSACTION;
					RETURN 1;
				END


			UPDATE NCOLLECTION_HIST
				SET [STATUS] = 'O' 
				WHERE NONZONE_REF=@NONZONE_REF 
				AND [STATUS] = 'L';

			SELECT @ErrorVar1 = @@error;

			IF @ErrorVar1 <> 0
				BEGIN
					ROLLBACK TRANSACTION;
					RETURN 1;
				END

			IF @STATUS='U'
				BEGIN
					SELECT @STATUS='L';
				END

			UPDATE NCOLLECTION
				SET	REF_DATE=@REF_DATE, DDREFNO=@DDREFNO, AMOUNT=@AMOUNT, VAT=@VAT, 
				COMMISION=@COMMISION, POSTAGE_CHARGE=@POSTAGE_CHARGE, 
				INSTRUMENT_EXP=@INSTRUMENT_EXP, RTN_STATUS=@RTN_STATUS, 
				RTN_CODE=@RTN_CODE, 
				INPUT_BY=@INPUT_BY,	INPUT_DATETIME=@INPUT_DATETIME,
				INPUT_FROM=@INPUT_FROM,
				AUTH_BY=substring(suser_name(),charindex('\',suser_name())+1,20),
				AUTH_DATETIME=GETDATE(),AUTH_FROM=HOST_NAME(),
				MOD_NO=@MOD_NO,[STATUS]=@STATUS
			WHERE NONZONE_REF=@NONZONE_REF;

			SELECT @ErrorVar1 = @@error;

			IF @ErrorVar1 <> 0
				BEGIN
					ROLLBACK TRANSACTION;
					RETURN 1;
				END
			
			DELETE NCOLLECTION_HIST WHERE NONZONE_REF=@NONZONE_REF AND IS_AUTH=0;

			SELECT @ErrorVar1 = @@error;

			IF @ErrorVar1 <> 0
				BEGIN
					ROLLBACK TRANSACTION;
					RETURN 1;
				END	

		END;
	
		

    -- Update CCHECK TOTAL AND CCHECK record

	SELECT @D_CODE = D_CODE 
		FROM CCHECK 
		WHERE NONZONE_REF =@NONZONE_REF
 	
	DELETE CCHECKTOTAL_HIST
		WHERE D_CODE=@D_CODE AND IS_AUTH=0;
            
	INSERT INTO CCHECKTOTAL_HIST 
		SELECT D_CODE,TOTAL_CHKNO,TOTAL_CHKAMT,
			INPUT_BY,INPUT_DATETIME,INPUT_FROM,
			AUTH_BY,AUTH_DATETIME,AUTH_FROM,MOD_NO,[STATUS],1
			FROM CCHECKTOTAL
			WHERE D_CODE =@D_CODE

	IF @ErrorVar1 <> 0
		BEGIN
			ROLLBACK TRANSACTION;
			RETURN 1;
		END

	UPDATE CCHECKTOTAL_HIST 
		SET [STATUS] = 'O' 
		WHERE D_CODE=@D_CODE AND [STATUS] = 'L';

	SELECT @ErrorVar1 = @@error;

	IF @ErrorVar1 <> 0
		BEGIN
			ROLLBACK TRANSACTION;
			RETURN 1;
		END

	UPDATE CCHECKTOTAL 
		SET INPUT_BY=@INPUT_BY,INPUT_DATETIME=@INPUT_DATETIME,
		INPUT_FROM=@INPUT_FROM,
		AUTH_BY=substring(suser_name(),charindex('\',suser_name())+1,20),
		AUTH_DATETIME=GETDATE(),AUTH_FROM=HOST_NAME(), MOD_NO=MOD_NO+1 
		WHERE D_CODE=@D_CODE;

	SELECT @ErrorVar1 = @@error;

	IF @ErrorVar1 <> 0
		BEGIN
			ROLLBACK TRANSACTION;
			RETURN 1;
		END
            
	DELETE CCHECK_HIST
		WHERE D_CODE=@D_CODE AND IS_AUTH=0;

	INSERT INTO CCHECK_HIST(SLNO, D_CODE, CLIENT_CODE, DLOCATION_CODE, 
		CUSTOMER_REF, SLIP_DATE, VALUE_DATE, CHECK_NO, AMOUNT, CHECK_DATE,
		CHECK_TYPE, DRAWER, DRAWEE_BANK_CODE, LOCATION_CODE, BRANCH_CODE, 
		CHECK_REF, RTN_STATUS, NONZONE_REF, MOD_NO, IS_AUTH, 
		REMINDER_DATE, REMINDER_NO, RTN_CODE)
	    SELECT SLNO, D_CODE, CLIENT_CODE, DLOCATION_CODE, 
		CUSTOMER_REF, SLIP_DATE, VALUE_DATE, CHECK_NO, AMOUNT, CHECK_DATE, 
		CHECK_TYPE, DRAWER, DRAWEE_BANK_CODE, LOCATION_CODE, BRANCH_CODE, 
		CHECK_REF, RTN_STATUS, NONZONE_REF, MOD_NO,1, 
		REMINDER_DATE, REMINDER_NO, RTN_CODE 
		FROM CCHECK
		WHERE D_CODE=@D_CODE;

	SELECT @ErrorVar1 = @@error;

	IF @ErrorVar1 <> 0
		BEGIN
			ROLLBACK TRANSACTION;
			RETURN 1;
		END

	UPDATE CCHECK SET MOD_NO=MOD_NO+1 
		WHERE D_CODE=@D_CODE;

	SELECT @ErrorVar1 = @@error;

	IF @ErrorVar1 <> 0
		BEGIN
			ROLLBACK TRANSACTION;
			RETURN 1;
		END

  
   
   --UPDATE CCHECK RECORD

	
	SELECT @CC_RTN_STATUS=RTN_STATUS,@CC_RTN_CODE=RTN_CODE 
			FROM NCOLLECTION_CCHECK_HIST 
			WHERE NONZONE_REF=@NONZONE_REF AND MOD_NO=@MOD_NO;
 
	IF @CC_RTN_STATUS='O'
		BEGIN
			UPDATE CCHECK SET RTN_STATUS='O', RTN_CODE=NULL,
				[FILENAME]=NULL ,FILE_USER_ID=NULL,FLEXGEN_DATE=NULL
				WHERE NONZONE_REF=@NONZONE_REF;
		END
	ELSE
		BEGIN
			UPDATE CCHECK SET RTN_STATUS=@CC_RTN_STATUS, 
				RTN_CODE=@CC_RTN_CODE 
				WHERE NONZONE_REF=@NONZONE_REF;		
		END
		
	

	UPDATE NCOLLECTION_CCHECK_HIST 
		SET IS_AUTH=1 
		WHERE NONZONE_REF=@NONZONE_REF AND MOD_NO=@MOD_NO;

	COMMIT TRANSACTION;
		
	SELECT @ErrorVar1 = @@error;
		
	IF @ErrorVar1 = 0
		BEGIN		
			RETURN 0;
		END
	ELSE
		BEGIN
			RETURN 1;
		END

    
END

GO

